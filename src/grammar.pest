WHITESPACE = _{ " " | "#" ~ (!("\n") ~ ANY)* ~ "\n"+ }

nl = _{ NEWLINE* }

alpha = @{ 'a'..'z' | 'A'..'Z' }
digit = @{ '0'..'9' }

// Expressions

// digit may not be first char
ident = @{
  (alpha | "_") ~ (alpha | digit | "_")* 
}

integer = @{ ("-")? ~ digit+ }

float = @{ ("-")? ~ ( digit+ ~ "." ~ digit* | "." ~ digit+ ) }

varg = ${ "$" ~ digit+ }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = _{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

pattern = { string }

rule = { pattern ~ "=>" ~ data_types }

data_types = _{ string | float | integer } // date is technically a data type

tuple = { "(" ~ nl ~ expr? ~ ("," ~ nl ~ expr)* ~ nl ~ ")" }

expr = _{ tuple | rule | integer | string | float | ident | varg }

// Main structure

command = { ident ~ expr* }

chain = _{ nl ~ ">>" ~ nl ~ subflow }

subflow = _{ command ~ chain? }

flow = { subflow }

file = _{ SOI ~ nl ~ flow ~ nl ~ EOI }
